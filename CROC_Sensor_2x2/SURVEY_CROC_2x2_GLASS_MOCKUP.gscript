VERSION v2.1
######################## SURVEY CROC Assembly Quad Sensor ROC #############################
# Jaiveer Dutta, jsdutta@purdue.edu
# Purdue University Department of Physics and Astronomy
# October 2023

# Adree Das, das165@purdue.edu
# June 2024
# Modifying original script to:
# i) use glass plates temporarily instead of actual sensors
# ii) Calculate the values for 4 joint ROCS and not independent modules
# -----------------------------------------------------
# Steps:
# 14. Prompt what station the survey will occur at
# 15. Calculate the placement of the HDI and Sensor ROC bondpads on each corner
# 16. Move gantry head to the module and survey for accuracy
# 17. End program with vacuums on or off

# Initialize the system
LOADCONFIG
HOME
SETVAC croc2x2_weight_0 0

# Dimensions of the HDI and the Sensor ROC
FLEXREAD $thickness_croc2x2HDI geometry.TFPX_CROC2X2_HDI.thickness
FLEXREAD $thickness_sensorROC geometry.TFPX_CROC2x2_SENSORROC.thickness
FLEXREAD $thickness_sensor geometry.TFPX_CROC2x2_SENSOR.thickness

# Dimensions of bond pads of the HDI and the Sensor ROC
FLEXREAD $dim_croc2x2_HDI geometry.TFPX_CROC2X2_HDI.fid_br
#MUL $dim_croc2x2_HDI_half $dim_croc2x2_HDI {0.5,0.5,0}
FLEXREAD $dim_croc2x2_SENSORROC geometry.TFPX_CROC2X2_SENSORROC.fid_br
FLEXREAD $len_hdi_bondpads geometry.dist_quadHDI_bondpads

# Dimensions of component ROCs in Sensor ROC
FLEXREAD $dist_quadROC_bondpads geometry.dist_quadROC_bondpads


# Parameters that can help line up the bond pads
#FLEXREAD $delta_tr siteGeometry.delta_croc2x2_tr
#FLEXREAD $delta_br siteGeometry.delta_croc2x2_br
#FLEXREAD $delta_bl siteGeometry.delta_croc2x2_bl
#FLEXREAD $delta_tl siteGeometry.delta_croc2x2_tl

## Continuing the script ##
# Step 14. Prompt what station the survey will occur at, then load appropriate data
@STEP_FOURTEEN GETINTPOPUP $station "Provide the Chuck number (0-4)"
GOTOIF @DISPLAY_ERROR `$station<0`
GOTOIF @DISPLAY_ERROR `$station>4`
LOADCONFIG quad "Logs\LOG_OUTPUT_CROC_Sensor_2x2_station_{$station}.txt"
GOTO @FLEXREADING
@DISPLAY_ERROR CHOICEPOPUP $trash "You gave me a bad number >:(" "Sorry" "My bad"
GOTO @STEP_FOURTEEN

@FLEXREADING PASS
FLEXREAD $pos_hdi_final quad.pos_hdi_final
FLEXREAD $rot_hdi_final quad.rot_hdi_final

# Step 15. Calculate the placement of the HDI and Sensor ROC bondpads on each corner
@STEP_FIFTEEN PASS

SIN $sin_rot $rot_hdi_final
COS $cos_rot $rot_hdi_final

  #Displacement vector for bondpads
MUL $bondpads_sin $dist_quadROC_bondpads $sin_rot
MUL $bondpads_cos $dist_quadROC_bondpads $cos_rot
COPY $v_bondpads {0,0,0}
SUB $v_bondpads.x $v_bondpads.x $bondpads_sin
COPY $v_bondpads.y $bondpads_cos

  #CROC
CALL @CALCCORNERS $pos_hdi_final $rot_hdi_final $dim_croc2x2_SENSORROC -> $pos_TR_SENROC_top $pos_BR_SENROC_bot $pos_BL_SENROC_bot $pos_TL_SENROC_top
ADD $pos_TR_SENROC_bot $pos_TR_SENROC_top $v_bondpads
SUB $pos_BR_SENROC_top $pos_BR_SENROC_bot $v_bondpads
SUB $pos_BL_SENROC_top $pos_BL_SENROC_bot $v_bondpads
ADD $pos_TL_SENROC_bot $pos_TL_SENROC_top $v_bondpads

  #HDI
SUB $pos_hdi_final.z $pos_hdi_final.z $thickness_croc2x2HDI
CALL @CALCCORNERS $pos_hdi_final $rot_hdi_final $dim_croc2x2_HDI -> $pos_TR_hdi_top $pos_BR_hdi_bot $pos_BL_hdi_bot $pos_TL_hdi_top
ADD $pos_TR_hdi_bot $pos_TR_hdi_top $v_bondpads
SUB $pos_BR_hdi_top $pos_BR_hdi_bot $v_bondpads
SUB $pos_BL_hdi_top $pos_BL_hdi_bot $v_bondpads
ADD $pos_TL_hdi_bot $pos_TL_hdi_top $v_bondpads

# Step 16. Move gantry head to the module and survey for accuracy
@STEP_SIXTEEN PASS
@ACC_0 CHOICEPOPUP $choice "Ready to survey the Top Right CROC's accuracy?"
GOTOIFN @ACC_0 $choice
CALL @QUARTERSURVEILLANCE $pos_TR_hdi_top $pos_BR_hdi_bot $pos_TR_SENROC_top $pos_BR_SENROC_bot -1 -> $ddx $ady

SETLOG "LOG_SURVEY_CROC_Sensor_2x2_station_{$station}.txt"
PRINT "---------------------"
PRINT "Survey Results"
PRINT "Delta(delta(X)): %f" $ddx
PRINT "Avg(delta(Y)): %f" $ady

HOME

# Step 17. End program with vacuums on or off
@STEP_SEVENTEEN PASS
CHOICEPOPUP $choice "Has the glue been cured according to the sample previously set aside?" "Yes" "No"
GOTOIFN @STEP_SEVENTEEN $choice

SETVAC croc2x2_weight_{$station} 0

CHOICEPOPUP $choice "Have you moved the weight tool back to its home position?" "Yes" "No"
GOTOIFN @STEP_SEVENTEEN $choice

GOTOIF @SINGLEVAC `$station>0`

SETVAC croc2x2_chuck_0_tr 0
SETVAC croc2x2_chuck_0_br 0
SETVAC croc2x2_chuck_0_bl 0
SETVAC croc2x2_chuck_0_tl 0
GOTO @STEP_END

@SINGLEVAC PASS
SETVAC "croc2x2_chuck_{$station}" 0

@STEP_END END

# SURVEYCORNERS
# Arguments:
# $x_corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
#
# Returns:
# $x_corner - Updated position of the corner that is being looked for
@SURVEYCORNERS(x_corner)
  MOVESAFE $x_corner 100
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOS $x_corner
  RETURN $x_corner

# CALCCORNERS
# Arguments:
#  $center - center of object
#  $rotation - rotation of the object
#  $dimensions - dimensions of object (as a vector)
#
# Returns
#  $topRight - position of the top right corner
#  $botRight - position of the bottom right corner
#  $botLeft - position of the bottom left corner
#  $topLeft - position of the top left corner
@CALCCORNERS(center,rotation,dimensions)
  SIN $sin_angle $rotation
  COS $cos_angle $rotation
  COPY $width $dimensions.x
  COPY $length $dimensions.y
  MUL $w_cos $width $cos_angle
  MUL $w_sin $width $sin_angle
  MUL $l_cos $length $cos_angle
  MUL $l_sin $length $sin_angle

  ADD $d_x_TR $l_sin $w_cos
  ADD $topRight $center.x $d_x_TR
  SUB $d_y_TR $w_sin $l_cos
  ADD $topRight.y $center.y $d_y_TR
  COPY $topRight.z $center.z

  SUB $d_x_BR $w_cos $l_sin
  ADD $botRight $center.x $d_x_BR
  ADD $d_y_BR $w_sin $l_cos
  ADD $botRight.y $center.y $d_y_BR
  COPY $botRight.z $center.z

  ADD $d_x_BL $w_cos $l_sin
  SUB $botLeft $center.x $d_x_BL
  SUB $d_y_BL $l_cos $w_sin
  ADD $botLeft.y $center.y $d_y_BL
  COPY $botLeft.z $center.z

  SUB $d_x_TL $l_sin $w_cos
  ADD $topLeft $center.x $d_x_TL
  ADD $d_y_TL $l_cos $w_sin
  SUB $topLeft.y $center.y $d_y_TL
  COPY $topLeft.z $center.z

  RETURN $topRight $botRight $botLeft $topLeft

# QUARTERSURVEILLANCE
# Arguments:
#  $top_hdi - Position of the topmost right HDI bond pad
#  $bot_hdi - Position of the bottommost right HDI bond pad
#  $top_roc - Position of the top right glass corner
#  $bot_roc - Position of the bottom right glass corner
#  $sign    - +1 for left side ROCs, -1 for right side ROCs
#
# Returns:
#  $d_DeltaX   - opening angle
#  $avg_DeltaY - slide
@QUARTERSURVEILLANCE(top_hdi,bot_hdi,top_roc,bot_roc,sign)
  MOVESAFE $top_hdi 100

  # HDI bond pads, top and bottom
  @HDI_TOP_OUTER CHOICEPOPUP $choice "Ready to survey the HDI's uppermost bond pad's any corner?"
  GOTOIFN @HDI_TOP_OUTER $choice
  CALL @SURVEYCORNERS  $top_hdi -> $top_hdi_outer
  COPY $top_hdi_inner $top_hdi_outer
  MUL $len_bondHDI_signed $len_hdi_bondpads $sign
  ADD $top_hdi_inner.x $top_hdi_outer.x $len_bondHDI_signed
  @HDI_TOP_INNER CHOICEPOPUP $choice "Ready to survey the HDI's uppermost bond pad's opposite corner?"
  GOTOIFN @HDI_TOP_INNER $choice
  CALL @SURVEYCORNERS $top_hdi_inner -> $top_hdi_inner
  ADD $top_hdi $top_hdi_outer $top_hdi_inner
  MUL $top_hdi $top_hdi {0.5,0.5,0.5}
  COPY $bot_hdi.z $top_hdi.z

  @HDI_BOT_OUTER CHOICEPOPUP $choice  "Ready to survey the HDI's lowermost pad's any corner?"
  GOTOIFN @HDI_BOT_OUTER $choice
  CALL @SURVEYCORNERS  $bot_hdi -> $bot_hdi_outer
  COPY $bot_hdi_inner $bot_hdi_outer
  ADD $bot_hdi_inner.x $bot_hdi_outer.x $len_bondHDI_signed
  @HDI_BOT_INNER CHOICEPOPUP $choice  "Ready to survey the HDI's lowermost bond pad's opposite corner?"
  GOTOIFN @HDI_BOT_INNER $choice
  CALL @SURVEYCORNERS $bot_hdi_inner -> $bot_hdi_inner
  ADD $bot_hdi $bot_hdi_outer $bot_hdi_inner
  MUL $bot_hdi $bot_hdi {0.5,0.5,0.5}

  @ROC_TOP_1 CHOICEPOPUP $choice "Ready to survey one corner of the ROC's top corner bond pad?"
  GOTOIFN @ROC_TOP_1 $choice
  CALL @SURVEYCORNERS $top_roc -> $top_roc_1
  @ROC_TOP_2 CHOICEPOPUP $choice "Ready to survey the opposite corner of the ROC's top corner bond pad?"
  GOTOIFN @ROC_TOP_2 $choice
  CALL @SURVEYCORNERS $top_roc_1 -> $top_roc_2
  ADD $top_roc $top_roc_1 $top_roc_2
  MUL $top_roc $top_roc {0.5,0.5,0.5}
  COPY $bot_roc.z $top_roc.z

  @ROC_BOT_1 CHOICEPOPUP $choice "Ready to survey one corner of the ROC's bottom corner bond pad?"
  GOTOIFN @ROC_BOT_1 $choice
  CALL @SURVEYCORNERS $bot_roc -> $bot_roc_1
  @ROC_BOT_2 CHOICEPOPUP $choice "Ready to survey the opposite corner of the ROC's bottom corner bond pad?"
  GOTOIFN @ROC_BOT_2 $choice
  CALL @SURVEYCORNERS $bot_roc_1 -> $bot_roc_2
  ADD $bot_roc $bot_roc_1 $bot_roc_2
  MUL $bot_roc $bot_roc {0.5,0.5,0.5}

  CALL @SURVEYCALC $top_hdi $bot_hdi $top_roc -> $d_upper_x $d_upper_y
  CALL @SURVEYCALC $bot_hdi $top_hdi $bot_roc -> $d_lower_x $d_lower_y

  SUB $d_DeltaX $d_upper_x $d_lower_x
  ADD $avg_DeltaY $d_upper_y $d_lower_y
  MUL $avg_DeltaY $avg_DeltaY 0.5
  RETURN $d_DeltaX $avg_DeltaY

# SURVEYCALC
# Arguments:
#  $near_hdi - Center position for the nearest HDI bond pad
#  $far_hdi - Center position for the furthest HDI bond pad
#  $ROC - Center position for the current ROC bond pad
#
# Returns:
#  $delta_x
#  $delta_y
@SURVEYCALC(near_hdi,far_hdi,ROC)
  #Variables
  COPY $a $near_hdi.x
  COPY $b $near_hdi.y
  COPY $c $far_hdi.x
  COPY $d $far_hdi.y
  COPY $e $ROC.x
  COPY $f $ROC.y

  #Slope
  SUB $b_min_d $b $d
  SUB $a_min_c $a $c
  INVERT $a_min_c $a_min_c
  MUL $slope $b_min_d $a_min_c

  #Calulating numerator for x
  MUL $slope_2 $slope $slope
  MUL $top_one $a $slope_2
  SUB $f_min_b $f $b
  MUL $top_two $f_min_b $slope
  ADD $top_oneTwo $top_one $top_two
  ADD $top $top_oneTwo $e

  #Calculating denominator for x
  ADD $bottom $slope_2 1
  INVERT $bottom $bottom

  #Calculating x and y
  MUL $x $top $bottom
  INVERT $inv_slope $slope
  MUL $inv_slope $inv_slope -1
  SUB $x_min_e $x $e
  MUL $y_one $inv_slope $x_min_e
  ADD $y $y_one $f

  #Calculating deltas
  SUB $a_min_x $a $x
  SUB $b_min_y $b $y
  SUB $e_min_x $e $x
  SUB $f_min_y $f $y

  POW $a_min_x_2 $a_min_x 2
  POW $b_min_y_2 $b_min_y 2
  ADD $delta_y_2 $a_min_x_2 $b_min_y_2
  POW $delta_y $delta_y_2 0.5

  ABS $abs_b_min_y $b_min_y
  INVERT $abs_b_min_y $abs_b_min_y
  MUL $sign_b_min_y $b_min_y $abs_b_min_y
  MUL $delta_y $sign_b_min_y $delta_y

  #PRINT "Delta y: %f" $delta_y

  POW $e_min_x_2 $e_min_x 2
  POW $f_min_y_2 $f_min_y 2
  ADD $delta_x_2 $e_min_x_2 $f_min_y_2
  POW $delta_x $delta_x_2 0.5
  RETURN $delta_x $delta_y
