VERSION v2.1
######################## CROC Assembly 1x2 Thermal Mockups  #############################
# Steps:
# 1. Turn on the HDI's vacuum
# 2. Move the gantry to the HDI for surveillance
# 3. Prompt placement of vacuum tool on the HDI
# 4. Transfer vacuum from below the HDI to the Vacuum Tool
# 5. Prompt placement of Vacuum Tool bearing HDI onto the thermal mockups
# 6. Release vacuum of Vacuum Tool
# 7. Prompt replacement of Vacuum Tool to its original position
# 8. Move gantry head to the module and survey for accuracy
# 9. Calculate a two points and rotations from the initial and final bond pad locations
# 10. End program with vacuums on or off


#Initialize the system
LOADCONFIG
HOME
SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC croc_chuck_1_top 0
SETVAC croc_chuck_1_bot 0
SETVAC croc_chuck_3 0
SETVAC croc_hdipicker 0
SETLOG "LOG_HDI_Translation_CROC_DataGeneration.txt"
SETERRORMODE prompt

#Rough positions of the top bond pad on the CROC HDI
FLEXREAD $pos_croc_hdi_top1 siteGeometry.pos_croc_hdi_top1 #Chuck 3
COPY $pos_croc_final_top1 {61.890,527.611,93.320}


#Dimensions of the HDI set of bond pads
COPY $dist_top2bot {0,43.040,0}


## Main Function ##

# 1. Turn on the HDI's vacuum
@STEP_ONE CHOICEPOPUP $choice "Did you place the CROC HDI on Chuck 3?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_ONE $choice
SETVAC croc_chuck_3 1


# 2. Move the gantry to the HDI for surveillance
@STEP_TWO PASS
MOVENAME crocStaging 100
MOVESAFE $pos_croc_hdi_top1 100
@HDI_0 CHOICEPOPUP $choice "Ready to find a corner of the top bond pad on the HDI?" "Yes" "No"
GOTOIFN @HDI_0 $choice
CALL @SURVEYCORNERS $pos_croc_hdi_top1 -> $pos_croc_hdi_top1

COPY $pos_croc_hdi_top2 $pos_croc_hdi_top1
@HDI_1 CHOICEPOPUP $choice "Ready to find the opposite corner of the top bond pad on the HDI?" "Yes" "No"
GOTOIFN @HDI_1 $choice
CALL @SURVEYCORNERS $pos_croc_hdi_top2 -> $pos_croc_hdi_top2

ADD $pos_croc_hdi_bot1 $pos_croc_hdi_top1 $dist_top2bot
@HDI_2 CHOICEPOPUP $choice "Ready to find a corner of the bottom bond pad on the HDI?" "Yes" "No"
GOTOIFN @HDI_2 $choice
CALL @SURVEYCORNERS $pos_croc_hdi_bot1 -> $pos_croc_hdi_bot1

COPY $pos_croc_hdi_bot2 $pos_croc_hdi_bot1
@HDI_3 CHOICEPOPUP $choice "Ready to find the opposite corner of the top bond pad on the HDI?" "Yes" "No"
GOTOIFN @HDI_3 $choice
CALL @SURVEYCORNERS $pos_croc_hdi_bot2 -> $pos_croc_hdi_bot2

  # Getting the center of the bond pads
ADD $pos_croc_hdi_top $pos_croc_hdi_top1 $pos_croc_hdi_top2
MUL $pos_croc_hdi_top $pos_croc_hdi_top {0.5,0.5,0.5}
ADD $pos_croc_hdi_bot $pos_croc_hdi_bot1 $pos_croc_hdi_bot2
MUL $pos_croc_hdi_bot $pos_croc_hdi_bot {0.5,0.5,0.5}

MOVENAME crocStaging 100


# 3. Prompt placement of vacuum tool on the HDI
@STEP_THREE CHOICEPOPUP $choice "Have you placed the Vacuum Tool on the HDI on Chuck 3?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_THREE $choice


# 4. Transfer vacuum from below the HDI to the Vacuum Tool
@STEP_FOUR PASS
SETVAC croc_hdipicker 1
WAIT 1000
SETVAC croc_chuck_3 0


# 5. Prompt placement of Vacuum Tool bearing HDI onto the thermal mockups
@STEP_FIVE CHOICEPOPUP $choice "Did you place the HDI on Chuck 1?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE $choice
SETVAC croc_chuck_1_bot 1
SETVAC croc_chuck_1_top 1
WAIT 1000


# 6. Release vacuum of Vacuum Tool
@STEP_SIX PASS
SETVAC croc_hdipicker 0


# 7. Prompt replacement of Vacuum Tool to its original position
@STEP_SEVEN CHOICEPOPUP $choice "Did you place the Vacuum Tool on Chuck 4?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_SEVEN $choice


# 8. Move gantry head to the module and survey for accuracy
@STEP_EIGHT PASS
MOVESAFE $pos_croc_final_top1 100
@SURVEY_0 CHOICEPOPUP $choice "Ready to find a corner of the top bond pad on the HDI?" "Yes" "No"
GOTOIFN @SURVEY_0 $choice
CALL @SURVEYCORNERS $pos_croc_final_top1 -> $pos_croc_final_top1

COPY $pos_croc_final_top2 $pos_croc_final_top1
@SURVEY_1 CHOICEPOPUP $choice "Ready to find the opposite corner of the top bond pad on the HDI?" "Yes" "No"
GOTOIFN @SURVEY_1 $choice
CALL @SURVEYCORNERS $pos_croc_final_top2 -> $pos_croc_final_top2

ADD $pos_croc_final_bot1 $pos_croc_final_top1 $dist_top2bot
@SURVEY_2 CHOICEPOPUP $choice "Ready to find a corner of the bottom bond pad on the HDI?" "Yes" "No"
GOTOIFN @SURVEY_2 $choice
CALL @SURVEYCORNERS $pos_croc_final_bot1 -> $pos_croc_final_bot1

COPY $pos_croc_final_bot2 $pos_croc_final_bot1
@SURVEY_3 CHOICEPOPUP $choice "Ready to find the opposite corner of the top bond pad on the HDI?" "Yes" "No"
GOTOIFN @SURVEY_3 $choice
CALL @SURVEYCORNERS $pos_croc_final_bot2 -> $pos_croc_final_bot2

  # Getting the center of the bond pads
ADD $pos_croc_final_top $pos_croc_final_top1 $pos_croc_final_top2
MUL $pos_croc_final_top $pos_croc_final_top {0.5,0.5,0.5}
ADD $pos_croc_final_bot $pos_croc_final_bot1 $pos_croc_final_bot2
MUL $pos_croc_final_bot $pos_croc_final_bot {0.5,0.5,0.5}

# 9. Calculate a two points and rotations from the initial and final bond pad locations
@STEP_NINE PASS
  #Initial position calculations
CALL @CALCPOSANDROT $pos_croc_hdi_top $pos_croc_hdi_bot -> $initial_pos $initial_rot
  #Final position calculations
CALL @CALCPOSANDROT $pos_croc_final_top $pos_croc_final_bot -> $final_pos $final_rot

  #Calculate the vector from initial -> final
SUB $vect_translation $final_pos $initial_pos
SUB $rot_translation $final_rot $initial_rot

  #Print out the data
PRINT "HDI Translation Vector: %v" $vect_translation
PRINT "HDI Translation Rotation: %f" $rot_translation


# 10. End program with vacuums on or off
@STEP_TEN PASS
CHOICEPOPUP $choice "Do you want to leave the vacuums on?" "Yes, Leave them on" "No, Turn them off"
GOTOIF @STEP_END $choice

SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC croc_chuck_1_top 0
SETVAC croc_chuck_1_bot 0
SETVAC croc_chuck_3 0
SETVAC croc_hdipicker 0

@STEP_END HOME

END

# SURVEYCORNERS
# Arguments:
# $x_corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
#
# Returns:
# $x_corner - Updated position of the corner that is being looked for
@SURVEYCORNERS(x_corner)
  MOVESAFE $x_corner 100
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOS $x_corner
  RETURN $x_corner

# CALCPOSANDROT
# Arguments:
# $top_pos - position of the center of the top hdi bond pad
# $bot_pos - position of the center of the bottom hdi bond pad
#
# Returns:
# $pos - the center position between the two points
# $rot - the rotation of the hdi (knowing the gantry's rotational system)
@CALCPOSANDROT(top_pos,bot_pos)
  ADD $pos $top_pos $bot_pos
  MUL $pos $pos {0.5,0.5,0.5}

  SUB $x_input $bot_pos.y $top_pos.y
  SUB $y_input $top_pos.x $bot_pos.x
  ATAN2 $rot $x_input $y_input

  RETURN $pos $rot
